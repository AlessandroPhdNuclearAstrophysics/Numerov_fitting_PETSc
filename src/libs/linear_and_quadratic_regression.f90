SUBROUTINE LINEAR_REGRESSION(Y, X, NMIN, NMAX, M, Q)
  IMPLICIT NONE
  INTEGER, INTENT(IN) :: NMIN, NMAX
  DOUBLE PRECISION, INTENT(IN) :: Y(NMAX), X(NMAX)
  DOUBLE PRECISION, INTENT(OUT) :: M, Q
  INTEGER :: I, N
  DOUBLE PRECISION :: SX, SX2, SY, SXY, DENOM

  ! Compute number of points
  N = NMAX - NMIN + 1
  IF (N < 2) THEN
    PRINT *, "ERROR: NOT ENOUGH POINTS FOR LINEAR_REGRESSION"
    STOP
  END IF

  ! Initialize sums
  SX  = 0.0D0
  SX2 = 0.0D0
  SY  = 0.0D0
  SXY = 0.0D0

  ! Compute sums
  DO I = NMIN, NMAX
    SX  = SX  + X(I)
    SX2 = SX2 + X(I) * X(I)
    SY  = SY  + Y(I)
    SXY = SXY + X(I) * Y(I)
  END DO

  ! Compute denominator (check for zero)
  DENOM = N * SX2 - SX * SX
  IF (ABS(DENOM) < 1.0D-12) THEN
    PRINT *, "ERROR: LINEAR REGRESSION SINGULARITY (DIVISION BY ZERO)"
    STOP
  END IF

  ! Compute slope and intercept
  M = (N * SXY - SX * SY) / DENOM
  Q = (SY - M * SX) / N

  RETURN
END SUBROUTINE LINEAR_REGRESSION



SUBROUTINE QUADRATIC_REGRESSION(Y, X, NMIN, NMAX, A, B, C)
  IMPLICIT NONE
  INTEGER, INTENT(IN) :: NMIN, NMAX
  DOUBLE PRECISION, INTENT(IN) :: Y(NMAX), X(NMAX)
  DOUBLE PRECISION, INTENT(OUT) :: A, B, C
  INTEGER :: I, N
  DOUBLE PRECISION :: S4, S3, S2, S1, S2Y, S1Y, S0Y, DET

  ! Compute number of points
  N = NMAX - NMIN + 1
  IF (N < 3) THEN
    PRINT *, "ERROR: NOT ENOUGH POINTS FOR LINEAR_REGRESSION"
    STOP
  END IF

  ! Initialize sums
  S4  = 0.D0  
  S3  = 0.D0
  S2  = 0.D0
  S1  = 0.D0
  S2Y = 0.D0
  S1Y = 0.D0
  S0Y = 0.D0

  ! Compute sums
  DO I = NMIN, NMAX    
    S4  = S4  + X(I)**4
    S3  = S3  + X(I)**3
    S2  = S2  + X(I)**2
    S1  = S1  + X(I)
    S2Y = S2Y + X(I)**2*Y(I)
    S1Y = S1Y + X(I)*Y(I)
    S0Y = S0Y + Y(I)
  END DO

  DET = -S2**3 + 2*S1*S2*S3 - N*S3**2 - S1**2*S4 + N*S2*S4
  IF (ABS(DET).LT.1.D-15) STOP "DETERMINANT IS ZERO"

  A = S1*S1Y*S2 - S0Y*S2**2 - S1**2*S2Y + N*S2*S2Y + S0Y*S1*S3 - N*S1Y*S3
  B = -S1Y*S2**2 + S1*S2*S2Y + S0Y*S2*S3 - N*S2Y*S3 - S0Y*S1*S4 + N*S1Y*S4
  C = -S2**2*S2Y + S1Y*S2*S3 + S1*S2Y*S3 - S0Y*S3**2 - S1*S1Y*S4 + S0Y*S2*S4
  
  A = A/DET
  B = B/DET
  C = C/DET

  RETURN
END SUBROUTINE QUADRATIC_REGRESSION


  

SUBROUTINE LINEAR_REGRESSION_2 (Y, X1, X2, NMIN, NMAX, NUM_SAMPLEPOINTS, A, B)
  IMPLICIT NONE
  INTEGER, INTENT(IN) :: NMIN, NMAX, NUM_SAMPLEPOINTS
  DOUBLE PRECISION, INTENT(IN) :: Y(0:NMAX), X1(0:NMAX), X2(0:NMAX)
  DOUBLE PRECISION, INTENT(OUT) :: A, B
  INTEGER :: I, IDX(NUM_SAMPLEPOINTS)
  DOUBLE PRECISION :: S11, S12, S22, T1, T2, DET, INV_S11, INV_S12, INV_S22

  ! RANDOMLY SELECT SAMPLE POINTS
  CALL RANDOM_SEED()
  DO I = 1, NUM_SAMPLEPOINTS
    CALL RANDOM_NUMBER(DET)
    IDX(I) = NMIN + INT((NMAX - NMIN) * DET)
  END DO

  ! INITIALIZE SUMS
  S11 = 0.0D0; S12 = 0.0D0; S22 = 0.0D0
  T1 = 0.0D0; T2 = 0.0D0

  ! COMPUTE SUMMATIONS
  DO I = 1, NUM_SAMPLEPOINTS
    S11 = S11 + X1(IDX(I)) * X1(IDX(I))
    S12 = S12 + X1(IDX(I)) * X2(IDX(I))
    S22 = S22 + X2(IDX(I)) * X2(IDX(I))
    T1 = T1 + X1(IDX(I)) * Y(IDX(I))
    T2 = T2 + X2(IDX(I)) * Y(IDX(I))
  END DO

  ! COMPUTE DETERMINANT
  DET = S11 * S22 - S12 * S12

  IF (ABS(DET) < 1.0D-12) THEN
    PRINT *, 'MATRIX IS SINGULAR!'
    STOP
  END IF

  ! COMPUTE INVERSE MATRIX ELEMENTS
  INV_S11 = S22 / DET
  INV_S12 = -S12 / DET
  INV_S22 = S11 / DET

  ! SOLVE FOR COEFFICIENTS
  A = INV_S11 * T1 + INV_S12 * T2
  B = INV_S12 * T1 + INV_S22 * T2

END SUBROUTINE LINEAR_REGRESSION_2