SUBROUTINE NUMEROV_SCATTERING_SINGLE_CHANNEL(ENERGIES, OBSERVABLES, NE, L, S, J, IPOT, ILB, NC, CF, RF, PARAMETERS)
  IMPLICIT NONE
  INTEGER, PARAMETER :: NMAX=200000
  DOUBLE PRECISION, PARAMETER :: HBARC = 197.327D0
  DOUBLE PRECISION, PARAMETER :: PI = 4.D0*DATAN(1.D0)

  INTEGER, INTENT(IN) :: NE, IPOT, ILB, L, S, J, NC
  DOUBLE PRECISION, DIMENSION(NE), INTENT(IN) :: ENERGIES
  DOUBLE PRECISION, DIMENSION(NE), INTENT(OUT):: OBSERVABLES
  DOUBLE PRECISION, INTENT(IN) :: CF(NC), RF
  DOUBLE PRECISION, INTENT(OUT):: PARAMETERS(3)

  LOGICAL :: VCOUL=.TRUE.  !FORCES THE TRADITIONAL COULOMB INTERACTION
  INTEGER :: N, T
  INTEGER :: I, LL
  DOUBLE PRECISION :: RANGE, E, H, R, HTM
  DOUBLE PRECISION :: M1, M2, MMU
  DOUBLE PRECISION :: RR(0:NMAX), U(0:NMAX), V(0:NMAX)
  DOUBLE PRECISION, EXTERNAL :: SPH_BESSEL_J, SPH_BESSEL_Y
  DOUBLE PRECISION :: RM, DELTA, K, NORM
  DOUBLE PRECISION, DIMENSION(2,2) :: VV
  INTEGER :: IE
  DOUBLE PRECISION :: K2(NE), A, B, C
  COMMON / PARAMETERS / E, HTM, V, H, RR, RM, U, N, LL

  RANGE = 50.D0
  RM = RANGE
  H = 0.01D0
  E = 0.1d0
  LL = L
  M1 = 939.565D0
  M2 = 938.272D0

  MMU= M1*M2/(M1+M2)
  HTM= HBARC**2/(2*MMU)

! CHECKS 
  IF (J.LT.ABS(L-S).OR.J.GT.(L+S)) STOP "WRONG TRIPLET LSJ"
  IF (MOD(L+S,2).EQ.1) T=0
  IF (MOD(L+S,2).EQ.0) T=1
  
  N = INT(RANGE/H)
  IF (N.GT.NMAX) THEN
    WRITE(*,*) "TOO MANY POINTS: ", N, "/", NMAX
    STOP
  ENDIF
  IF (RM.GT.RANGE) THEN
    write(*,*) "RM: ", RM, " is greater than the range ", RANGE
    STOP
  ENDIF
!END CHECKS
  
  RR = 0.D0
  V  = 0.D0
  DO I=1, N
    R = I*H
    RR(I) = R
    CALL POT_PW(IPOT, ILB, 0, VCOUL, L, S, J, 1, -1, R, VV, CF, RF)
    V(I) = VV(1,1)
  ENDDO
  
  DO IE=1, NE
    E = ENERGIES(IE)
    CALL RUN_NUMEROV(NORM, DELTA)
    K = DSQRT(E/HTM)
    K2(IE) = K**2
    OBSERVABLES(IE) = K**(2*L+1)*DCOTAN(DELTA)
  ENDDO ! IE
  
  CALL QUADRATIC_REGRESSION(OBSERVABLES, K2, 1, NE, A, B, C)
  PARAMETERS(1) = C
  PARAMETERS(2) = B
  PARAMETERS(3) = A

END SUBROUTINE NUMEROV_SCATTERING_SINGLE_CHANNEL




SUBROUTINE RUN_NUMEROV(NORM, DELTA)
  IMPLICIT NONE
  INTEGER, PARAMETER :: NMAX = 200000
  DOUBLE PRECISION, PARAMETER :: PI = 4.D0*DATAN(1.D0)
  DOUBLE PRECISION, INTENT(OUT) :: NORM, DELTA
  INTEGER :: N, LL
  DOUBLE PRECISION :: H, E, HTM, RR(0:NMAX), U(0:NMAX), V(0:NMAX), RM
  COMMON / PARAMETERS / E, HTM, V, H, RR, RM, U, N, LL

  DOUBLE PRECISION :: K, R, F(0:NMAX), A
  INTEGER :: I, NM

  K = DSQRT(E/HTM)  ! in fm^-1
  F  = 0.D0
  DO I=1, N
    R = RR(I)
    F(I) = (E-V(I))/HTM - LL*(LL+1)/R**2
  ENDDO
  CALL NUMEROV_HOMOGENOEUS(H, U, F, 0.D0, 1.D-6, N, .TRUE.)

  ! NORMALIZATION PROCESS
  NM = INT(RM/H)
  CALL TWO_POINTS_NORMALIZATION(U, RR, NM-1, NM, K, LL, A, DELTA, NORM)

END SUBROUTINE RUN_NUMEROV









SUBROUTINE TWO_POINTS_NORMALIZATION(U, RVEC, N1, N2, K, L, A, DELTA, NORMALIZATION_CONSTANT)
  IMPLICIT NONE
  INTEGER, INTENT(IN) :: N1, N2, L
  DOUBLE PRECISION, DIMENSION(0:N2), INTENT(IN) :: RVEC, U
  DOUBLE PRECISION, INTENT(IN) :: K
  DOUBLE PRECISION, INTENT(OUT) :: A, DELTA, NORMALIZATION_CONSTANT
  DOUBLE PRECISION, EXTERNAL :: SPH_BESSEL_J, SPH_BESSEL_Y

  DOUBLE PRECISION :: R1, R2, UR1, UR2, J1, Y1, J2, Y2, TANDELTA

  IF (N1.GE.N2) THEN
    WRITE(*,*) "TWO_POINTS_NORMALIZATION: N1 MUST BE LESS THAN N2"
    STOP 
  ENDIF

  R1 = RVEC(N1)
  R2 = RVEC(N2)

  UR1 = U(N1)/(K*R1)
  J1 = SPH_BESSEL_J(L, K*R1)
  Y1 = SPH_BESSEL_Y(L, K*R1)
  
  UR2 = U(N2)/(K*R2)
  J2 = SPH_BESSEL_J(L, K*R2)
  Y2 = SPH_BESSEL_Y(L, K*R2)

  A =         (UR2*Y1 - UR1*Y2)/(J2*Y1 - J1*Y2)
  TANDELTA = -(J1*UR2-J2*UR1)/(UR1*Y2-UR2*Y1)
  DELTA = DATAN(TANDELTA)
  NORMALIZATION_CONSTANT = 1.D0/(A*DSQRT(1+TANDELTA**2))

END SUBROUTINE TWO_POINTS_NORMALIZATION